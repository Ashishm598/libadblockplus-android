import java.util.regex.Matcher
import java.util.regex.Pattern

// ABI flavor of "libadblockplus-android" module
def getAbiFlavor() {
  Gradle gradle = getGradle()
  String  taskReqStr = gradle.getStartParameter().getTaskRequests().toString()

  Pattern pattern;

  if (taskReqStr.contains("assemble")) {
    pattern = Pattern.compile("assemble(Abi_\\w{2,3})(Release|Debug)*(\\w*)")
  } else {
    pattern = Pattern.compile("generate(Abi_\\w{2,3})(Release|Debug)*(\\w*)")
  }

  Matcher matcher = pattern.matcher(taskReqStr)

  if (matcher.find()) {
    return matcher.group(1).toLowerCase()
  } else {
    return "abi_all"
  }
}

buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:2.3.0'
  }
}

def gradleBuildDir = System.getenv('GRADLE_BUILD_DIR')
if (gradleBuildDir != null) {
  println "[Configuration] Building project in ${gradleBuildDir}"
  allprojects {
    buildDir = "${gradleBuildDir}/${project.name}"
  }
}

subprojects {
  task listAllDependencies(type: DependencyReportTask) {}
}

println "[Configuration] Using libadblockplus-android ABI flavor: " + getAbiFlavor()
